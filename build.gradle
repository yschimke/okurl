buildscript {
    ext.kotlin_version = '1.2.21'
    repositories {
        maven { url "http://dl.bintray.com/kotlin/kotlin-dev" }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.shyiko:ktlint:0.15.0"
    }
}

plugins {
    id "com.gradle.build-scan" version "1.11"
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'net.nemerosa.versioning' version '2.6.1'
    id 'io.codearte.nexus-staging' version '0.11.0'
    id "org.jlleitschuh.gradle.ktlint" version "3.0.0"
}

buildScan { licenseAgreementUrl = 'https://gradle.com/terms-of-service'; licenseAgree = 'yes' }

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'distribution'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.baulsupp'

versioning {
    releaseMode = project.hasProperty('releaseMode') ? project.property('releaseMode') : 'snapshot'
}

version = versioning.info.display

description = """OkHttp Social Authentication"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes("Implementation-Title": "OkSocial", "Implementation-Version": version)
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

distTar {
    compression = Compression.GZIP
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar, jar
}

repositories {
    mavenLocal()
    maven { url 'http://repo.spring.io/milestone' }
    maven { url 'https://oss.jfrog.org/libs-snapshot' }
    maven { url 'https://dl.bintray.com/reactivesocket/RSocket' }
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/releases" }
    maven { url "http://dl.bintray.com/kotlin/kotlin-dev" }
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile("org.jetbrains.kotlin:kotlin-script-util:$kotlin_version") {
        exclude(module: 'kotlin-compiler')
    }
    compile "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"

    compile('com.baulsupp:oksocial-output:1.3.0')
    compile 'com.squareup.okhttp3:okhttp:3.10.0-SNAPSHOT'
    compile 'com.squareup.okhttp3:logging-interceptor:3.10.0-SNAPSHOT'
    compile 'org.conscrypt:conscrypt-openjdk-uber:1.0.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.4'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.4'
    compile('com.github.mrmike:ok2curl:0.4.0') {
        exclude(module: 'okhttp')
    }
    compile 'javax.activation:activation:1.1.1'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:slf4j-jdk14:1.7.25'
    compile 'org.zeroturnaround:zt-exec:1.10'
    compile 'com.jakewharton.byteunits:byteunits:0.9.1'
    compile 'io.netty:netty-resolver-dns:4.1.20.Final'
    compile 'io.airlift:airline:0.8'
    compile files('external/osxkeychain.jar')
    compile 'io.zipkin.reporter2:zipkin-sender-okhttp3:2.3.1'
    compile 'io.zipkin.brave:brave-okhttp:4.13.4'
    compile 'io.zipkin.brave:brave-instrumentation-okhttp3:4.14.1'
    compile 'io.zipkin.brave:brave:4.14.1'
    compile 'com.squareup.moshi:moshi:1.5.0'
    compile 'com.squareup.moshi:moshi-kotlin:1.5.0'
    compile 'com.squareup.moshi:moshi-adapters:1.5.0'
    compile 'org.brotli:dec:0.1.2'
    compile 'com.github.markusbernhardt:proxy-vole:1.0.4'

    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    testCompile 'com.squareup.okhttp3:okhttp-testing-support:3.9.1'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.9.1'
}

distributions {
    main {
        contents {
            from("${rootProject.projectDir}") {
                include "README.md", "LICENSE"
            }
            from("${rootProject.projectDir}/src/main/scripts") {
                fileMode 0755
                into "bin"
            }
            from("${rootProject.projectDir}/certificates") {
                into "certificates"
            }
            from("${rootProject.projectDir}/commands") {
                fileMode 0755
                into "bin"
            }
            from("${rootProject.projectDir}/bash") {
                into "bash"
            }
            from("${rootProject.projectDir}/src/main/resources") {
                into "scripts"
                include "oksocial.js"
            }
            into('lib') {
                from jar
            }
        }
    }
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.compilerArgs += '-parameters'
}

//packaging tests
task packageTests(type: Jar) {
    from sourceSets.test.output
    classifier = 'tests'
}

artifacts.archives packageTests

kotlin {
    experimental {
        coroutines "enable"
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

if (project.hasProperty('ossrhUser')) {
    signing {
        sign configurations.archives
    }

    nexusStaging {
        username = ossrhUser
        password = ossrhPassword
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUser, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUser, password: ossrhPassword)
                }

                pom.project {
                    name project.name
                    group 'com.baulsupp'
                    description "Command Line Output Library"
                    url "https://github.com/yschimke/oksocial"

                    scm {
                        connection 'scm:git:https://github.com/yschimke/oksocial.git'
                        developerConnection 'scm:git:git@github.com:yschimke/oksocial.git'
                        url 'https://github.com/yschimke/oksocial.git'
                    }

                    licenses {
                        license {
                            name 'Apache License'
                            url 'http://opensource.org/licenses/apache-2.0'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'yschimke'
                            name = 'Yuri Schimke'
                            email = 'yuri@schimke.ee'
                        }
                    }

                    packaging 'jar'
                }
            }
        }
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = (selection.candidate.group == "io.netty" && selection.candidate.version ==~ /5\..*/) || (selection.candidate.group == "org.jetbrains.kotlin" && selection.candidate.version ==~ /.*dev.*/)
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

task resolveDependencies {
    setDescription "Resolves all projects dependencies from the repository."
    setGroup "Build Server"

    doLast {
        rootProject.allprojects { project ->
            project.buildscript.configurations.forEach { configuration ->
                if (configuration.canBeResolved) {
                    configuration.resolve()
                }
            }

            project.configurations.forEach { configuration ->
                if (configuration.canBeResolved) {
                    configuration.resolve()
                }
            }
        }
    }
}
